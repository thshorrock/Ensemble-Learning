#
# Copyright (C) Troy D. Straszheim
#
# Distributed under the Boost Software License, Version 1.0.
# See accompanying file LICENSE_1_0.txt or copy at
#   http://www.boost.org/LICENSE_1_0.txt
#

#
# This file is generated by cmake and installed to 
# BOOST_VERSIONED_CMAKE_FILES_DIR/Boost-@BOOST_VERSION@.
# 
# when a users does a find_package(Boost ...) BoostConfigVersion.cmake
# and BoostVersion.cmake will search for this file.
#

#
#  Various variables
#
set(Boost_INCLUDE_DIRS "@CMAKE_INSTALL_PREFIX@/@BOOST_INCLUDE_INSTALL_DIR@")
set(Boost_INCLUDE_DIR "@CMAKE_INSTALL_PREFIX@/@BOOST_INCLUDE_INSTALL_DIR@"
  CACHE FILEPATH "Boost include directory")
set(Boost_LIBRARY_DIRS "@CMAKE_INSTALL_PREFIX@/@BOOST_LIB_INSTALL_DIR@")
#
#  Include the imported targets
#
include("@CMAKE_INSTALL_PREFIX@/@BOOST_INSTALL_EXPORTS_FILE@/Boost.cmake")

#
#  Calculate target suffix from user variables
#
if (NOT DEFINED Boost_USE_MULTITHREADED)
  set(Boost_USE_MULTITHREADED TRUE)
endif()


if (Boost_USE_MULTITHREADED)
  set(mtflag "-mt")
  set(mtdesc "multi-threaded")
else()
  set(mtflag "")
  set(mtdesc "single-threaded")
endif()

if (Boost_USE_STATIC)
  set(staticflag "-static")
  set(staticdesc "statically-linked")
else()
  set(staticflag "-shared")
  set(staticdesc "dynamically-linked")
endif()

set(Boost_LIBRARIES "")
foreach(component ${Boost_FIND_COMPONENTS})
  #
  # Check that it is really a target
  # 
  set(target boost_${component}${mtflag}${staticflag})
  get_target_property(p ${target} TYPE)
  if (NOT (p MATCHES "_LIBRARY$"))
    message(STATUS "Library boost_${component} (${mtdesc}, ${staticdesc}) is not available in the detected Boost @BOOST_VERSION@ installation under @CMAKE_INSTALL_PREFIX@.")
  else()
    list(APPEND Boost_LIBRARIES "optimized;${target}")
  endif()


  get_target_property(p ${target}-debug TYPE)
  if (NOT (p MATCHES "_LIBRARY$"))
    message(SEND_ERROR "Library boost_${component} (${mtdesc}, ${staticdesc}) is not available in the detected Boost @BOOST_VERSION@ installation under @CMAKE_INSTALL_PREFIX@.")
  else()
    list(APPEND Boost_LIBRARIES "debug;${target}-debug")
  endif()

endforeach()

